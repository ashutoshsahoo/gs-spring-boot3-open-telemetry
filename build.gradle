plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.palantir.docker' version '0.36.0' apply false
}

group 'com.ashu.practice'
version '1.3.0-SNAPSHOT'


ext {
    springdocOpenapiVersion = '2.6.0'
    logbookVersion = '3.10.0'
    lokiLogbackVersion = '1.5.2'
    swaggerAnnotationsVersion = '2.2.25'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = '21'
    }


    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
        dependencies {
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocOpenapiVersion"
            dependency "org.zalando:logbook-spring-boot-starter:$logbookVersion"
            dependency "com.github.loki4j:loki-logback-appender:$lokiLogbackVersion"
            dependency "io.swagger.core.v3:swagger-annotations:$swaggerAnnotationsVersion"
        }

    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}